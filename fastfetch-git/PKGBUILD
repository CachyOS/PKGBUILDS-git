# Maintainer: Linus Dierheimer <Linus@Dierheimer.de>

pkgname=fastfetch-git
pkgver=r399.1717b51
pkgrel=1
pkgdesc="Like neofetch, but much faster because written in c"
arch=("any")
url="https://github.com/LinusDierheimer/fastfetch"
license=("MIT")
depends=()
makedepends=(
  "git"
  "cmake"
  "pciutils"
  "libxrandr" # Depends on libX11, which headers are also needed
  "dconf"     # Depends on glib2, which headers are also needed
  "wayland"
  "sqlite"
)
optdepends=(
  "pciutils: GPU output"
  "libx11: Basic resolution output"
  "libxrandr: Multi monitor and refresh rate support for resolution output"
  "wayland: Native wayland support for resolution output"
  "glib2: Output for values that are only stored in GSettings"
  "dconf: Output for values that are only stored in DConf"
  "xfconf: XFWM theme + xfce-terminal font"
  "sqlite: rpm package count"
)
_provides_and_conflicts=(
  "fastfetch"
  "fastfetch-garuda"
  "flashfetch"
)
provides=("${_provides_and_conflicts[@]}")
conflicts=("${_provides_and_conflicts[@]}")
source=("git+https://github.com/LinusDierheimer/fastfetch.git")
sha256sums=("SKIP")

pkgver() {
  cd "fastfetch/"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build()
{
    cd "${srcdir}/fastfetch"
    mkdir -p build/
    cd build/
    cmake ..
    cmake --build .
}

package() {
    cd ${srcdir}/fastfetch

    install -D "build/fastfetch" "${pkgdir}/usr/bin/fastfetch"
    install -D "build/flashfetch" "${pkgdir}/usr/bin/flashfetch"
    install -D "completions/bash" "${pkgdir}/usr/share/bash-completion/completions/fastfetch"

    install -Dd "${pkgdir}/usr/share/fastfetch/presets/"
    for file in presets/*; do
      install -D "$file" "${pkgdir}/usr/share/fastfetch/presets/"
    done
}
